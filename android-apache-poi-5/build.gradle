plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
    id 'maven-publish'
}

group 'pl.gocards'
version "$android_apache_poi_version"

dependencies {
    implementation 'org.apache.poi:poi-ooxml:5.2.3'
    implementation 'xerces:xercesImpl:2.12.2'
    implementation 'sax:sax:2.0.1'
}

shadowJar {
    dependencies {
        // https://mvnrepository.com/artifact/org.apache.poi/poi/5.2.3
        include(dependency('org.apache.poi:poi:5.2.3'))
        include(dependency('org.apache.poi:poi-ooxml:5.2.3'))
        include(dependency('org.apache.poi:poi-ooxml-lite:5.2.3'))
        include(dependency('org.apache.xmlbeans:xmlbeans:5.1.1'))
    }

    archiveBaseName.set('poi-5.2.3')
    exclude 'repackage/**'
    relocate 'java.awt', 'org.apache.poi.java.awt'
}

jar.dependsOn shadowJar
test.dependsOn shadowJar

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}